

/*

CREATE TABLE IF NOT EXISTS `ero_daily_rewards` (
	`id` INT(11) UNSIGNED NOT NULL AUTO_INCREMENT,
	`aid` INT(11) UNSIGNED NOT NULL,
	`status` TINYINT(11) UNSIGNED NOT NULL DEFAULT '1',
	`time` DATETIME NOT NULL DEFAULT '0000-00-00 00:00:00',
	PRIMARY KEY (`id`),
	KEY (`aid`)
) ENGINE=MyISAM;

*/

prontera,154,184,5	script	Daily#npc	4_F_KAFRA9,{
	doevent "daily_login_reward::OnTalk";
}


-	script	daily_login_reward	-1,{
	function reward_list;
	
	function	reward_list	{
		.@give_reward = getarg( 0,0 );
		.@argcount = getargcount() - 2;
		
		mes "Today rewards:";
		if ( .@argcount >= 1 ) {
			for ( .@i = 1; .@i <= .@argcount; .@i += 2 ) {
				.@item_id = getarg( .@i,0 );
				.@amount = getarg( .@i+1,0 );
				
				mes .@amount +"x "+getitemname( .@item_id );
				if ( .@give_reward )
					getitem getarg( .@i ),getarg( .@i + 1 );
			}
			if ( .@give_reward )
				#online_reward_dayofyear = .dayofyear;
				
				
				.@give_reward = ( #online_reward_dayofyear != .dayofyear && .dayofyear > 0 && #online_minute >= .minute );
				
			if ( !.@give_reward && #online_minute && #online_minute < .minute ) {
				mes " ";
				mes "You still need to accumulate another total of "+( .minute - #online_minute )+" minute(s) before you can get the rewards.";
			}
		}
		else {
			mes "> none";
		}
		return;
	}
	
	OnInit:
		// duration to count as completed daily login.
		.minute = 60;
		// check interval
		.interval_check_sec = 60;
		// idle stop reward
		.idle_sec = 300;
		// cutin name format.
		.cutin_name_format$ = "login_day_%02d";
		.npc_name$ = strnpcinfo(0);
		bindatcmd "dailies",.npc_name$+"::OnTalk";
		
	OnHour00:
		.today = atoi( gettimestr( "%Y%m%d",10 ) );
		.dayofyear = gettime(8);
		// .@is_reset = ( gettime(5) == 1 );
		// if ( .@is_reset ) {
		// 	query_sql( "UPDATE `ero_daily_rewards` SET `status` = 2 WHERE `status` = 1" );
		// }
		callsub( L_restart, .@is_reset );
		end;
		
	OnTalk:
		query_sql( "SELECT COUNT(`aid`) FROM `ero_daily_rewards` WHERE `aid` = "+getcharid(3)+" AND `status` <> 2",.@size );
		
		if ( .cutin_name_format$ != "" )
			cutin sprintf( .cutin_name_format$,.@size ),4;
			
		mes "[Daily Rewards]";
		mes "Total Daily Login = "+.@size+" Day(s)";
		mes " ";
		next;
		if ( select( "Claim Rewards","Cancel" ) == 1 ) {

				.@give_reward = ( #online_reward_dayofyear != .dayofyear && .dayofyear > 0 && #online_minute >= .minute );
				
				mes "[Daily Rewards]";
				switch( .@size ) {
					case 0:
						mes "You have to reach the minimum online playtime to complete the daily cycle.";
						mes " ";
					// case X: reward_list( <itemid>,<amount>,...,<item>,<amount> ); break;
					case 1: reward_list( .@give_reward, 12405,10 ); break;	// Unripe Ygg Seed 10x
					case 2: reward_list( .@give_reward, 12016,25 ); break;	// Speed Potion 25x
					case 3: reward_list( .@give_reward, 12114,10 ); break;	// Elemental Enchanter - Fire x 10
					case 4: reward_list( .@give_reward, 12115,10 ); break;	// Elemental Enchanter - Water x 10
					case 5: reward_list( .@give_reward, 12116,10 ); break;	// Elemental Enchanter - Earth x 10
					case 6: reward_list( .@give_reward, 12217,10 ); break;	// Elemental Enchanter - Wind x 10
					case 7: reward_list( .@give_reward, 12016,50 ); break;	// Speed Potion 50x
					case 8: reward_list( .@give_reward, 12019,1 ); break;	// Holy Egg x1
					case 9: reward_list( .@give_reward, 7621,1 ); break;	// Token of Siegfried 1x	
					case 10: reward_list( .@give_reward, 14590,5); break;	// Level 5 Assumptio Party Scroll x5
					case 11: reward_list( .@give_reward, 14588,5,14589,5 ); break;	// Level 10 Party Blessing and Agi Scrolls x5
					case 12: reward_list( .@give_reward, 12103,2 ); break;	// Bloody Branch 2x
					case 13: reward_list( .@give_reward, 616,5 ); break;	// Old Card Album x5
					case 14: reward_list( .@give_reward, 7621,2 ); break;	// Token of Siegfried 2x
					case 15: reward_list( .@give_reward, 12214,1 ); break;	// Convex Mirror x1
					case 16: reward_list( .@give_reward, 12695,2 ); break;	// Accessory Card Album 2x
					case 17: reward_list( .@give_reward, 12691,2 ); break;	// Armor Card Album 2x
					case 18: reward_list( .@give_reward, 12208,5 ); break;	// Battle Manual 50 percent 30 minutes
					case 19: reward_list( .@give_reward, 12214,5 ); break;	// Convex Mirror 5x
					case 20: reward_list( .@give_reward, 12031,20,12030,20 ); break;	// Box of Drowsiness and Resentment x 20
					case 21: reward_list( .@give_reward, 7621,5 ); break;	// Token of Siegfried 5x
					case 22: reward_list( .@give_reward, 12214,5 ); break;	// Convex Mirror x5
					case 23: reward_list( .@give_reward, 12516,5 ); break;	// Small Life Potion x5
					case 24: reward_list( .@give_reward, 12129,15 ); break;	// Fantastic Cooking Kit x15
					case 25: reward_list( .@give_reward, 14545,1 ); break;		// Field Manual 300 percent x1
					case 26: reward_list( .@give_reward, 616,10 ); break;		// Old Card Album 10
					case 27: reward_list( .@give_reward, 12214,10 ); break;		// Convex Mirror 10x
					case 28: reward_list( .@give_reward, 7621,5 ); break;		// Token of Siegfried 5x
					case 29: reward_list( .@give_reward, 12623,10 ); break;		// Advanced Weapons Box x10
					case 30: reward_list( .@give_reward, 12103,5 ); break;		// Bloody Branch 5x
					case 31: reward_list( .@give_reward, 12246,10 ); break;		// Mystical Card Album x10
					default:
						mes "No rewards available for this day.";
						close;
				}
				
				if ( .@size && #online_reward_dayofyear == .dayofyear ) {
					next;
					mes "[Daily Rewards]";
					mes "You've claimed the rewards for "+.@size+" day(s), try again tomorrow.";
				}
		}
		close;
		
	OnUpdate:
		if ( checkidle() < .idle_sec ) {
			#online_minute++;
			
			if ( #online_minute >= .minute && #online_reward_dayofyear != .today ) {
				query_sql( "INSERT INTO `ero_daily_rewards` ( `aid`,`time` ) VALUES ( "+getcharid(3)+",NOW() )" );
				dispbottom "You have completed daily login for today.";
			}
			else {
				dispbottom "Daily Reward start counting # currently "+#online_minute+"/"+.minute+" minutes.";
			}
		}
		
	OnPCLoginEvent:
		if ( #online_reward_dayofyear != .dayofyear && .dayofyear > 0 ) {
			if ( #online_reward_dayofyear ) {
				#online_reward_dayofyear = 0;
				#online_minute = 0;
			}
			if ( #online_minute < .minute ) {
				deltimer .npc_name$+"::OnUpdate";
				addtimer ( .interval_check_sec * 1000 ),.npc_name$+"::OnUpdate";
			}
			
			// if ( .cutin_name_format$ != "" ) {
			// 	query_sql( "SELECT COUNT(`aid`) FROM `ero_daily_rewards` WHERE `aid` = "+getcharid(3)+" AND `status` <> 2",.@size );
			// 	cutin sprintf( .cutin_name_format$,.@size ),4;
			// }
		}
		end;
		
	L_restart:
		.@value = getarg( 0,0 );
		
		query_sql( "SELECT `account_id` FROM `char` WHERE `online` = 1",.@aid );
		.@aid_size = getarraysize( .@aid );
		for ( .@i = 0; .@i < .@aid_size; .@i++ )
			if ( attachrid( .@aid[.@i] ) ) {
				#online_minute = 0;
				doevent .npc_name$+"::OnPCLoginEvent";
			}
		end;
}




